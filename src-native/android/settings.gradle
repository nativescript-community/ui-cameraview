import groovy.json.JsonSlurper

pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        def USER_PROJECT_ROOT = "$rootDir/../.."
        def widgetsDir = "$USER_PROJECT_ROOT/node_modules/@nativescript/core/platforms/android"
        def appPackageJsonFile = file("$USER_PROJECT_ROOT/package.json")
        if (appPackageJsonFile.exists()) {
            def appPackageJson = new JsonSlurper().parseText(appPackageJsonFile.text)
            if (appPackageJson.dependencies['@akylas/nativescript'] != null || appPackageJson.devDependencies['@akylas/nativescript'] != null) {
                widgetsDir = "$USER_PROJECT_ROOT/node_modules/@akylas/nativescript/platforms/android"
            }
        }
        println("widgetsDir $widgetsDir")
        flatDir {
            dirs "$widgetsDir"
        }
        google()
        mavenCentral()
    }
}
include 'cameraviewdemo'
include 'ui-camera'
include ':camerademoapp'
